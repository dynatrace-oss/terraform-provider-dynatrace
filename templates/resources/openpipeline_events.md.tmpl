---
layout: ""
page_title: "dynatrace_openpipeline_events Resource - terraform-provider-dynatrace"
subcategory: "OpenPipeline"
description: |-
  The resource `dynatrace_openpipeline_events` covers configuration of OpenPipeline for Events
---

# dynatrace_openpipeline_events (Resource)

!> Deploying an OpenPipeline configuration will overwrite the existing one of the same kind, causing any manual changes made in the web UI or other configurations managed by Terraform or Monaco to be lost. Ensure all configurations are defined within a single Terraform or Monaco configuration to prevent data loss.

-> **Dynatrace SaaS only**

-> To utilize this resource, please define the environment variables `DT_CLIENT_ID`, `DT_CLIENT_SECRET`, `DT_ACCOUNT_ID` with an OAuth client including the following permissions: **View OpenPipeline configurations** (`openpipeline:configurations:read`), and **Edit OpenPipeline configurations** (`openpipeline:configurations:write`).

## Dynatrace Documentation

- OpenPipeline - https://docs.dynatrace.com/docs/platform/openpipeline

## Export Example Usage

- `terraform-provider-dynatrace -export dynatrace_openpipeline_events` downloads all existing OpenPipeline definitions for Events

The full documentation of the export feature is available [here](https://dt-url.net/h203qmc).

## Resource Example Usage

```terraform
resource "dynatrace_openpipeline_events" "events" {
  endpoints {
    endpoint {
      enabled        = true
      default_bucket = "default_events"
      display_name   = "Custom ingest source"
      segment        = "something"
      routing {
        type        = "static"
        pipeline_id = "default"
      }
    }
  }
  pipelines {
    pipeline {
      enabled      = true
      display_name = "Custom pipeline 1"
      id           = "pipeline_Pipeline_8075"
      processing {
        processor {
          fields_add_processor {
            description = "Add a field 1"
            enabled     = true
            id          = "processor_Add_a_field_6856"
            matcher     = "true"
            field {
              name  = "field"
              value = "value"
            }
          }
        }
      }
    }
    pipeline {
      enabled      = true
      display_name = "Custom pipeline 2"
      id           = "pipeline_Pipeline_8076"
      data_extraction {
        processor {
          davis_event_extraction_processor {
            description = "Custom event"
            enabled     = true
            id          = "processor_Custom_event_3193"
            matcher     = "true"
            properties {
              key   = "event.type"
              value = "CUSTOM_ALERT"
            }
            properties {
              key   = "event.name"
              value = "test"
            }
          }
        }
      }
      metric_extraction {
        processor {
          value_metric_extraction_processor {
            description = "Custom value metric extraction"
            enabled     = true
            dimensions  = [ "availability" ]
            field       = "field1"
            id          = "processor_Custom_metric_extraction_7786"
            matcher     = "true"
            metric_key  = "events.custom"
          }
        }
        processor {
          counter_metric_extraction_processor {
            description = "Custom counter metric extraction"
            enabled     = true
            id          = "processor_Custom_counter_metric_extraction_2885"
            matcher     = "true"
            metric_key  = "events.counter"
          }
        }
      }
      processing {
        processor {
          fields_add_processor {
            description = "Custom add field"
            enabled     = true
            id          = "processor_Add_a_field_6856"
            matcher     = "true"
            field {
              name  = "field"
              value = "value"
            }
          }
        }
        processor {
          fields_rename_processor {
            description = "Custom rename field"
            enabled     = true
            id          = "processor_Custom_rename_field_7712"
            matcher     = "true"
            field {
              from_name = "new"
              to_name   = "old"
            }
          }
        }
        processor {
          fields_remove_processor {
            description = "Custom remove field"
            enabled     = true
            fields      = [ "field" ]
            id          = "processor_Custom_remove_field_7320"
            matcher     = "true"
          }
        }
        processor {
          dql_processor {
            description = "Custom DQL"
            enabled     = true
            dql_script  = "fieldsAdd (\"test\")"
            id          = "processor_Custom_DQL_1783"
            matcher     = "true"
          }
        }
      }
      security_context {
        processor {
          security_context_processor {
            description = "Custom security contet"
            enabled     = true
            id          = "processor_Custom_security_contet_4309"
            matcher     = "true"
            value {
              type     = "constant"
              constant = "string"
            }
          }
        }
        processor {
          security_context_processor {
            description = "Custom security context 2"
            enabled     = true
            id          = "processor_Custom_security_context_2_9052"
            matcher     = "true"
            value {
              type  = "field"
              field = "fieldname"
            }
          }
        }
        processor {
          security_context_processor {
            description = "Custom security context 3"
            enabled     = true
            id          = "processor_Custom_security_context_2_9053"
            matcher     = "true"
            value {
              type  = "multiValueConstant"
              multi_value_constant = ["multi", "value"]
            }
          }
        }
      }
      storage {
        catch_all_bucket_name = "default_events"
        processor {
          bucket_assignment_processor {
            description = "Custom bucket assignment"
            enabled     = true
            bucket_name = "default_events"
            id          = "processor_Custom_bucket_assignment_5664"
            matcher     = "true"
          }
        }
        processor {
          no_storage_processor {
            description = "Custom no storage assignment"
            enabled     = true
            id          = "processor_Custom_no_storage_assignment_2070"
            matcher     = "true"
          }
        }
      }
    }
  }
  routing {
    entry {
      enabled     = true
      matcher     = "true "
      note        = "Custom route"
      pipeline_id = "pipeline_Pipeline_8075"
    }
  }
}
```

{{ .SchemaMarkdown | trimspace }}
