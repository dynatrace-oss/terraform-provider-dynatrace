---
layout: ""
page_title: "dynatrace_aws_connection_role_arn Resource - terraform-provider-dynatrace"
subcategory: "Connections"
description: |-
  The resource `dynatrace_aws_connection_role_arn` covers configuration for AWS connections
---

# dynatrace_aws_connection_role_arn (Resource)

-> This resource requires the API token scopes **Read settings** (`settings.read`) and **Write settings** (`settings.write`)

-> This resource requires the OAuth scopes **Read settings** (`settings:objects:read`) and **Write settings** (`settings:objects:write`)

## Requirements
This resource must be used in combination with the `dynatrace_aws_connection` resource.
Ensure you configure both resources together for a valid AWS connection.
An example of how to set up both resources can be found in the [Resource Example Usage](#resource-example-usage) section below.

## Limitations
~> **Warning** If a resource is created using an API token or without setting `DYNATRACE_HTTP_OAUTH_PREFERENCE=true` (when both are used), the settings object's owner will remain empty.

An empty owner implies:
- The settings object becomes public, allowing other users with settings permissions to read and modify it.
- Changing the settings object's permissions will have no effect, meaning the `dynatrace_settings_permissions` resource can't alter its access.

When a settings object is created using platform credentials:
- The owner is set to the owner of the OAuth client or platform token.
- By default, the settings object is private; only the owner can read and modify it.
- Access modifiers can be managed using the `dynatrace_settings_permissions` resource.

We recommend using platform credentials to ensure a correct setup.
In case an API token is needed, we recommend setting `DYNATRACE_HTTP_OAUTH_PREFERENCE=true`.

## Dynatrace Documentation

- AWS Connector - https://docs.dynatrace.com/docs/analyze-explore-automate/workflows/actions/aws

- Settings API - https://www.dynatrace.com/support/help/dynatrace-api/environment-api/settings (schemaId: `builtin:hyperscaler-authentication.connections.aws`)

## Export Example Usage

- `terraform-provider-dynatrace -export dynatrace_aws_connection_role_arn` downloads all existing AWS connections.

The full documentation of the export feature is available [here](https://dt-url.net/h203qmc).

## Resource Example Usage

```terraform
resource "dynatrace_aws_connection" "test-aws-connection" {
  name = "#name#"
  web_identity {
    consumers = ["APP:dynatrace.aws.connector"]
  }
}

resource "aws_iam_openid_connect_provider" "dynatrace-oidc-provider" {
  url = "https://token.dynatrace.com"

  client_id_list = [
    "<TENANT_URL>/app-id/dynatrace.aws.connector",
  ]
}

resource "aws_iam_role" "example_role" {
  name = "#name#"
  assume_role_policy = jsonencode(
    {
        "Version": "2012-10-17",
        "Statement": [
            {
                "Effect": "Allow",
                "Principal": {
                    "Federated": "${aws_iam_openid_connect_provider.dynatrace-oidc-provider.arn}"
                },
                "Action": "sts:AssumeRoleWithWebIdentity",
                "Condition": {
                    "StringEquals": {
                        "${aws_iam_openid_connect_provider.dynatrace-oidc-provider.url}:sub": "dt:connection-id/${dynatrace_aws_connection.test-aws-connection.id}",
                        "${aws_iam_openid_connect_provider.dynatrace-oidc-provider.url}:aud": "<TENANT_URL>/app-id/dynatrace.aws.connector"
                    }
                }
            }
        ]
    })
}

resource "dynatrace_aws_connection_role_arn" "test-aws-connection-arn" {
  aws_connection_id = dynatrace_aws_connection.test-aws-connection.id
  role_arn          = aws_iam_role.example_role.arn
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `aws_connection_id` (String) The ID of a `dynatrace_aws_connection` resource instance for which to define the AWS Role ARN
- `role_arn` (String) The ARN of the AWS role that should be assumed.

### Optional

- `timeouts` (Block, Optional) (see [below for nested schema](#nestedblock--timeouts))

### Read-Only

- `id` (String) The ID of this resource.

<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- `create` (String)
